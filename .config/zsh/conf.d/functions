# vim: filetype=zsh
function path() {
    print -l $path
}

compdef hg=rg
function hg() {
    kitty +kitten hyperlinked_grep --smart-case "$@"
}

compdef dot=git
function dot() {
    local options=(-C $HOME --git-dir=.dotfiles --work-tree=.)
    local sub_options=()

    case "$1" in
    st|status)
        sub_options+=--untracked-files=no
        ;;
    esac

    git $options $@ $sub_options
}

function beer() {
    case "$1" in
    install)
        brew bundle install --global --no-upgrade
        ;;
    up)
        brew bundle install --global
        ;;
    status)
        print -P "Running %F{green}brew bundle check%f\n";
        brew bundle check --global --verbose
        print -P "\nRunning %F{green}brew outdated%f\n";
        brew outdated
        print -P "\nRunning %F{green}brew autoremove --dry-run%f\n";
        brew autoremove --dry-run
        ;;
    clean)
        brew cleanup -s
        brew autoremove
        ;;
    sync)
        brew bundle install --global --no-upgrade --cleanup --zap
        ;;
    *)
        print -P "beer - Manage Homebrew dependencies specified by ~/.Brewfile\n"
        print -P "%F{yellow}USAGE:"
        print -P -aC 2 \
            "%F{green}beer install"     "%fInstall dependencies" \
            "%F{green}beer up"          "%fUpgrade dependencies" \
            "%F{green}beer status"      "%fCheck for outdated, missing, or extraneous dependencies" \
            "%F{green}beer sync"        "%fSame as install (additionally removing unspecified dependencies)" \
            "%F{green}beer clean"       "%fRemoves extraneous dependencies. Scrubs the Homebrew cache"
        ;;
    esac
}
