function path() {
    print -l $path
}

compdef hg=rg
function hg() {
    kitty +kitten hyperlinked_grep --smart-case "$@"
}

compdef dot=git
function dot() {
    local options=(-C $HOME --git-dir=.dotfiles --work-tree=.)
    local sub_options=()

    case "$1" in
    st|status)
        sub_options+=--untracked-files=no
        ;;
    esac

    git $options $@ $sub_options
}

function beer() {
    case "$1" in
    install)
        brew bundle install --global --no-upgrade
        ;;
    check)
        brew bundle check --global --verbose
        brew outdated
        ;;
    scrub)
        brew cleanup -s
        ;;
    sync)
        brew bundle install --global --no-upgrade --cleanup --zap
        brew autoremove
        ;;
    up)
        brew bundle install --global
        brew upgrade
        ;;
    *)
        print -P "beer - Manage Homebrew dependencies specified by ~/.Brewfile\n"
        print -P "%F{yellow}USAGE:"
        print -P -aC 2 \
            "%F{green}beer install"     "%fInstall dependencies" \
            "%F{green}beer check"       "%fCheck for outdated/missing dependencies" \
            "%F{green}beer up"          "%fUpgrade dependencies" \
            "%F{green}beer sync"        "%fSame as install (additionally removing unspecified dependencies)" \
            "%F{green}beer scrub"       "%fScrubs the Homebrew cache"
        ;;
    esac
}
