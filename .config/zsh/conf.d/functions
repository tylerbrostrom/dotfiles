# vim: filetype=zsh
function path() {
    print -l $path
}

compdef hg=rg
function hg() {
    kitty +kitten hyperlinked_grep --smart-case "$@"
}

compdef dot=git
function dot() {
    local options=(-C $HOME --git-dir=.dotfiles --work-tree=.)
    local sub_options=()

    case "$1" in
    st|status)
        sub_options+=--untracked-files=no
        ;;
    esac

    git $options $@ $sub_options
}

function beer() {
    case "$1" in
    up)
        brew bundle install --global
        ;;
    sync)
        brew bundle install --global --no-upgrade --cleanup --zap
        ;;
    check)
        print -P "%F{yellow}OUTDATED / MISSING DEPS:%f"
        brew bundle check --global --verbose
        print -P "\n%F{yellow}UNSPECIFIED DEPS:%f"
        brew bundle cleanup --global
        ;;
    ls)
        print -P "%F{yellow}FORMULAS:%f"
        brew bundle list --global --formula
        print -P "\n%F{yellow}CASKS:%f"
        brew bundle list --global --cask
        print -P "\n%F{yellow}TAPS:%f"
        brew bundle list --global --tap
        ;;
    ls-tree)
        brew bundle list --global --formula | xargs brew deps --installed --formula --tree --include-requirements
        ;;
    *)
        print -P "beer - Manage global Homebrew dependencies specified by ~/.Brewfile\n"
        print -P "%F{yellow}USAGE:"
        print -P -aC 2 \
            "%F{green}beer up%f"          "Upgrade outdated deps; install missing deps" \
            "%F{green}beer sync%f"        "Remove unspecified deps; install missing deps" \
            "%F{green}beer check%f"       "Check for outdated, missing, or unspecified deps" \
            "%F{green}beer ls%f"          "List deps by type: formula, cask, tap" \
            "%F{green}beer ls-tree%f"     "List formulas and their dependencies"
        ;;
    esac
}
