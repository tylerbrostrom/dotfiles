function path() {
    print -l $path
}

compdef hg=rg
function hg() {
    kitty +kitten hyperlinked_grep --smart-case "$@"
}

compdef dot=git
function dot() {
    local options=(-C $HOME --git-dir=.dotfiles --work-tree=.)
    local sub_options=()

    case "$1" in
    st|status)
        sub_options+=--untracked-files=no
        ;;
    esac

    git $options $@ $sub_options
}

function beer() {
    case "$1" in
    install)
        brew update
        brew bundle install --global --no-upgrade
        ;;
    up)
        brew update
        brew bundle install --global
        ;;
    check)
        brew update
        brew bundle check --global --verbose
        ;;
    clean)
        brew bundle cleanup --global --force --zap
        brew cleanup --prune=all
        brew autoremove
        ;;
    *)
        print -P "beer - Manage Homebrew dependencies, as per ~/.Brewfile\n"
        print -P "%F{yellow}USAGE:"
        print -P -aC 2 \
            "%F{green}beer install"         "%fInstall dependencies" \
            "%F{green}beer up"      "%fUpgrade dependencies" \
            "%F{green}beer check"     "%fCheck for missing dependencies" \
            "%F{green}beer clean"   "%fUninstall dependencies not listed in ~/.Brewfile"
        ;;
    esac
}
